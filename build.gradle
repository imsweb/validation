plugins {
    id 'java-library'
    id 'checkstyle'
    id 'com.github.spotbugs' version '4.5.1'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

group = 'com.imsweb'
version = file('VERSION').text.trim()
description = 'This framework allows edits to be defined in Groovy and to be executed on various data types.'
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    api 'com.imsweb:staging-client-java:6.0.0'
    api 'org.codehaus.groovy:groovy:3.0.8:indy'
    api 'com.thoughtworks.xstream:xstream:1.4.16'

    implementation 'commons-codec:commons-codec:1.15'
    implementation 'org.apache.commons:commons-lang3:3.11'

    testImplementation 'junit:junit:4.13'
    testImplementation 'commons-io:commons-io:2.7'
    testImplementation 'com.imsweb:staging-algorithm-cs:02.05.50.7'
    testImplementation 'com.imsweb:staging-algorithm-tnm:1.9.6'
    testImplementation 'com.imsweb:staging-algorithm-eod-public:2.0.8'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.validation'
        )
    }
}

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
    configProperties = ['suppressionFile' : project(':').file('config/checkstyle/checkstyle-exclude.xml')]
}

// spotbugs plugin settings
spotbugs {
    ignoreFailures = true
    excludeFilter =  project(':').file("config/spotbugs/spotbugs-exclude.xml")
}

// use this task to generate the regex parser
task generateJavaContextParser {
    doLast {
        javaexec {
            main = '-jar'
            args = ['config/jflex-1.4.2/lib/JFlex.jar',
                    '--nobak',
                    '-d',
                    'src/main/java/com/imsweb/validation/internal/context/',
                    'config/java-context.flex']
        }
    }
}

// configure nexus staging plugin
nexusStaging {
    numberOfRetries = 50
    delayBetweenRetriesInMillis = 5000
}

// needed to deploy to Maven Central
modifyPom {
    project {
        name 'Validation Framework'
        description 'This framework allows edits to be defined in Groovy and to be executed on various data types.'
        url 'https://github.com/imsweb/validation'
        inceptionYear '2015'

        scm {
            url 'https://github.com/imsweb/validation'
            connection 'scm:https://github.com/imsweb/validation.git'
            developerConnection 'scm:git@github.com:imsweb/validation.git'
        }

        licenses {
            license {
                name 'A modified BSD License (BSD)'
                url 'https://github.com/imsweb/validation/blob/master/LICENSE'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'depryf'
                name 'Fabian Depry'
                email 'depryf@imsweb.com'
            }
        }
    }
}

// don't try to release a snapshot to a non-snapshot repository, that won't work anyway
if (version.endsWith('-SNAPSHOT'))
    gradle.startParameter.excludedTaskNames += 'closeAndReleaseRepository'

// Gradle wrapper, this allows to build the project without having to install Gradle!
wrapper {
    gradleVersion = '6.7'
    distributionType = Wrapper.DistributionType.ALL
}


